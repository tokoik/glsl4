#version 120

// texture.frag

// ラスタライザから受け取る頂点の位置の補間値
varying vec4 position;

// ラスタライザから受け取る頂点の法線ベクトルの補間値
varying vec3 normal;

// テクスチャのサンプラ
uniform sampler2D texture;

void main ()
{
  // 法線ベクトル
  vec3 fnormal = normalize(normal);

  // 光線ベクトル
  vec3 light = normalize((gl_LightSource[0].position * position.w
    - gl_LightSource[0].position.w * position).xyz);

  // 視線ベクトル
  vec3 view = -normalize(position.xyz);

  // 中間ベクトル
  vec3 halfway = normalize(light + view);

  // 拡散反射率
  float diffuse = max(dot(fnormal, light), 0.0);

  // 鏡面反射率
  float specular = pow(max(dot(fnormal, halfway), 0.0), gl_FrontMaterial.shininess);

  // テクスチャから画素の色を得る
  vec4 color = texture2DProj(texture, gl_TexCoord[0]);

  // フラグメントの色
  gl_FragColor = gl_LightSource[0].ambient * color
               + gl_LightSource[0].diffuse * diffuse * color
               + gl_FrontLightProduct[0].specular * specular;
}
